start_datetime: year month day hour? minute? second?
start_timedelta: duration_day? duration_hour? duration_minute? duration_second? (BEFORE_TIME | AFTER_TIME)?

year: number "年"
month: number "月"
day: number "日"
hour: number "時"
minute: number "分"
second: number "秒"

duration_day: number "日" | number "日間"
duration_hour: number "時" | number "時間"
duration_minute: number "分" | number "分間"
duration_second: number "秒" | number "秒間"

BEFORE_TIME: "前"
AFTER_TIME: "後"

number: (MINUS | PLUS)? mixed_number_with_unit
mixed_number_with_unit: unit_oku? unit_man? unit_sen? unit_hyaku? unit_juu? mixed_number?

MINUS: "-" | "ー" | "マイナス" | "ひく" | "引く"
PLUS: "+" | "＋" | "プラス" | "たす" | "足す"

unit_oku: mixed_number OKU // 億は数値が必須
unit_man: mixed_number MAN // 万は数値が必須
unit_sen: mixed_number? SEN
unit_hyaku: mixed_number? HYAKU
unit_juu: mixed_number? JU

OKU: "億"
MAN: "万" | "萬"
SEN: "千" | "阡"
HYAKU: "百" | "佰"
JU: "十" | "拾"

mixed_number: (ZENKAKU_DIGIT | KANJI_DIGIT | DIGIT)+ // 漢字混合で小数点のサポートは行わない
KANJI_DIGIT: "一" | "壱"
           | "二" | "弐"
           | "三" | "参"
           | "四" | "肆"
           | "五" | "伍"
           | "六" | "陸"
           | "七" | "漆"
           | "八" | "捌"
           | "九" | "玖"
           | "〇" | "ゼロ" | "零"
           | "○" | "◯" // 一応丸もサポートしておく
ZENKAKU_DIGIT: "０" | "１" | "２" | "３" | "４" | "５" | "６" | "７" | "８" | "９"

%import common.DIGIT
%import common.WS
%ignore WS
%ignore ","
